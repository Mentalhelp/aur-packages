# Contributer: v2punkt0 <v2punkt0@gmail.com>

pkgname='dwm-hg'
pkgver=1575
pkgrel=1
pkgdesc="The latest hg pull of dwm"
url="http://dwm.suckless.org"
license=('MIT')
arch=('i686' 'x86_64')
depends=('libx11')
makedepends=('mercurial' 'sed')
conflicts=('dwm')
provides=('dwm')
source=('http://dwm.suckless.org/patches/nmaster-ncol.c'
        'config.h'
        'monocle-no-border.patch')
md5sums=('279b014d22674cfc5eab60398c5d3a2e'
         '30271563fc414270b7fdcc85e67729ab'
         'ea218fd4823ea9b5cbf6b129473b3a2a')


_hgroot='http://hg.suckless.org'
_hgrepo='dwm'

build() {
    cd $srcdir

    # update the repo, else clone a new one
    if [ -d $_hgrepo ]; then
        cd $_hgrepo
        make clean
        hg pull -u
    else
        hg clone ${_hgroot}/${_hgrepo}
        cd $_hgrepo
    fi

    cp $srcdir/config.h .
    cp $srcdir/nmaster-ncol.c .
    patch -p1 < $srcdir/monocle-no-border.patch

    # add correct settings to config.mk
    sed -i "s|^PREFIX =.*|PREFIX = /usr|" config.mk
    sed -i "s|^X11INC =.*|X11INC = /usr/include/X11|" config.mk
    sed -i "s|^X11LIB =.*|X11LIB = /usr/lib/X11|" config.mk

    msg "Starting build process."
    make || return 1
}

package() {
    cd $srcdir/$_hgrepo

    make PREFIX=$pkgdir/usr install

    mkdir -p $pkgdir/usr/share/licenses/dwm
    cp LICENSE $pkgdir/usr/share/licenses/dwm
}
