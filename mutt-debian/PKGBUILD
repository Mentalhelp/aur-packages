# Contributor: tobias [tobias [at] archlinux.org]
# Maintainer: Jakob Matthes <jakob.matthes@gmail.com>

# Credits to: Gaetan Bisson <bisson@archlinux.org>

# Derived from extra/mutt/PKGBUILD.

pkgname=mutt-debian-git
_gitname=mutt
pkgver=1.5.21
pkgrel=1
pkgdesc='Small but very powerful text-based mail client'
url='http://www.mutt.org/'
license=('GPL')
backup=('etc/Muttrc')
arch=('i686' 'x86_64')
conflicts=('mutt')
provides=('mutt')
replaces=('mutt')
depends=('gpgme' 'ncurses' 'openssl' 'libsasl' 'gdbm' 'libidn' 'mime-types' 'krb5')
source=('git+https://github.com/jakobmatthes/mutt-debian#branch=patches')
md5sums=('SKIP')

pkgver() {
	cd $_gitname
	git describe --always --tags | sed 's|-|.|g'
}

prepare() {
	cd $_gitname

	autoreconf -fi

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-gpgme \
		--enable-hcache \
		--enable-imap \
		--enable-pop \
		--enable-smtp \
		--with-curses \
		--with-idn \
		--with-sasl \
		--with-gss \
		--with-regex \
		--with-ssl
}

build() {
	cd $_gitname
	make
}

package() {
	cd $_gitname
	make DESTDIR="${pkgdir}" install
	rm "${pkgdir}"/usr/bin/{flea,muttbug}
	rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1
	rm "${pkgdir}"/etc/mime.types{,.dist}
	install -Dm644 contrib/gpg.rc "${pkgdir}"/etc/Muttrc.gpg.dist
}

# vim:noet ts=2 ft=sh
