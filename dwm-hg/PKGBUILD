# Contributer: v2punkt0 <v2punkt0@gmail.com>

pkgname='dwm-hg'
pkgver=1575
pkgrel=1
pkgdesc="The latest hg pull of dwm"
url="http://dwm.suckless.org"
license=('MIT')
arch=('i686' 'x86_64')
depends=('libx11' 'libxinerama')
makedepends=('mercurial')
conflicts=('dwm')
provides=('dwm')
source=('http://dwm.suckless.org/patches/nmaster-ncol.c'
        'config.h'
        'monocle-no-border.patch')
md5sums=('279b014d22674cfc5eab60398c5d3a2e'
         'd3f2b312c1c9a0f3c1969f4a797a95dc'
         'ea218fd4823ea9b5cbf6b129473b3a2a')


_hgroot='http://hg.suckless.org'
_hgrepo='dwm'

build() {
    cd $srcdir
    msg "Connecting to Mercurial server...."

    if [[ -d "$_hgrepo" ]]; then
        cd "$_hgrepo"
        hg pull -u
        msg "The local files are updated."
    else
        hg clone "$_hgroot" "$_hgrepo"
    fi

    msg "Mercurial checkout done or server timeout"
    msg "Starting build..."

    rm -rf "$srcdir/$_hgrepo-build"
    cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
    cd "$srcdir/$_hgrepo-build"

    cp $srcdir/config.h .
    cp $srcdir/nmaster-ncol.c .
    patch -p1 < $srcdir/monocle-no-border.patch

    sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
    sed -i 's/^CFLAGS = -g/#CFLAGS += -g/g' config.mk
    sed -i 's/^#CFLAGS = -std/CFLAGS += -std/g' config.mk
    sed -i 's/^LDFLAGS = -g/#LDFLAGS += -g/g' config.mk
    sed -i 's/^#LDFLAGS = -s/LDFLAGS += -s/g' config.mk
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd $srcdir/$_hgrepo-build
    make PREFIX=/usr DESTDIR=$pkgdir install
    install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
    install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
}
